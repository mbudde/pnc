# This is the pnc prelude

,e     { 2.71828182845904523536028747135266250 } def
,pi    { 3.14159265358979323846264338327950288 } def
,sqrt2 { 1.41421356237309504880168872420969808 } def

,+ ,add   alias
,- ,sub   alias
,. ,mul   alias
,/ ,div   alias
#,% ,mod   alias
,s ,swap  alias
,d ,dup   alias
,p ,print alias
,– ,stdin alias
,× ,mul   alias
,· ,mul   alias
,@ ,map   alias

,peek { dup print } def
,pp ,peek alias

,++ { 1 + } def
,-- { 1 - } def

,sum { 0 { + } fold } def
,product { 1 { . } fold } def
,/+ ,sum alias
,/. ,product alias

,1/ { 1 swap div } def

,abs { dup sign . } def
,sign { 0 cmp } def
,eq { cmp abs 1 swap - } def
,gt { cmp 0 max } def
,ge { -- cmp 0 max } def
,lt { swap cmp 0 max } def
,le { ++ swap cmp 0 max } def

# Delta percent
,d% { over - s / } def

,avg {
    dup
    len
    swap
    sum
    swap
    div
} def

,fib {
    0 swap
    1 swap
    { dup roll3 + } swap
    repeat
    pop
} def

,range {
    [
        arg   # from
        arg   # to
        swap over -
        d 0 ge
        {
            { dup ++ } swap repeat
        }
        { pop pop }
        if
    ]
} def

,seq ,range alias
,.. ,range alias

,upto { 1 swap range } def

,! { seq /. } def

,choose { over over - ! s ! . s ! s / } def


,vmax {
    { max } fold1
} def
,vmin {
    { min } fold1
} def
